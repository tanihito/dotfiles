## Alias
#
case "${OSTYPE}" in
freebsd*|darwin*)
  alias ls="ls -G -w"
  ;;
linux*)
  alias ls="ls --color"
  alias open="xdg-open"
  ;;
esac

alias j="jobs -l"

alias l="ls -la"
alias la="ls -a"
alias ll="ls -l"

alias du="du -h"
alias df="df -h"

alias su="su -l"

alias ack="ack-grep"

alias -g L="| less"
alias -g G='| grep'
alias -g C='| cat -n'
alias -g W='| wc'
alias -g H='| head'
alias -g T='| tail'

alias _='sudo'


## Completion
#
setopt complete_aliases
fpath=(${HOME}/.zsh/functions/Completion ${fpath})
autoload -U compinit
compinit


## Environment variables
#
export LANG=ja_JP.UTF-8
case ${UID} in
0)
  LANG=C
  ;;
esac
export EDITOR=vi


## Theme and appearance
#
autoload -Uz colors
colors
setopt prompt_subst
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}

PROMPT=$'%{$fg_bold[green]%}%n@%m:%~%{$reset_color%} %{$fg[blue]%}%D{[%I:%M:%S]} %{$reset_color%}$(git_prompt_info)\
%{$fg_bold[blue]%}%#%{$reset_color%} '

ZSH_THEME_GIT_PROMPT_PREFIX="%{$fg[green]%}["
ZSH_THEME_GIT_PROMPT_SUFFIX="]%{$reset_color%}"
ZSH_THEME_GIT_PROMPT_DIRTY=" %{$fg[red]%}*%{$fg[green]%}"
ZSH_THEME_GIT_PROMPT_CLEAN=""


## Directories
#
setopt auto_name_dirs
setopt auto_pushd
setopt pushd_ignore_dups
alias -g ...='../..'
alias -g ....='../../..'


# Correction
#
setopt correct_all
alias man='nocorrect man'
alias mv='nocorrect mv'
alias mysql='nocorrect mysql'
alias mkdir='nocorrect mkdir'
alias gist='nocorrect gist'
alias heroku='nocorrect heroku'
alias ebuild='nocorrect ebuild'
alias hpodder='nocorrect hpodder'


## Keybind configuration
#

# emacs like keybind (e.x. Ctrl-a gets to line head and Ctrl-e gets
# to end) and something additions
bindkey -e
bindkey "^[[1~" beginning-of-line # Home gets to line head
bindkey "^[[4~" end-of-line # End gets to line end
bindkey "^[[3~" delete-char # Del

# historical backward/forward search with linehead string binded to ^P/^N
autoload history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^p" history-beginning-search-backward-end
bindkey "^n" history-beginning-search-forward-end
bindkey "\\ep" history-beginning-search-backward-end
bindkey "\\en" history-beginning-search-forward-end

# reverse menu completion binded to Shift-Tab
bindkey "\e[Z" reverse-menu-complete


## History
#
HISTFILE=$HOME/.zsh_history
HISTSIZE=10000
SAVEHIST=10000

setopt append_history
setopt extended_history
setopt hist_expire_dups_first
setopt hist_ignore_dups # ignore duplication command history list
setopt hist_ignore_space
setopt hist_verify
setopt inc_append_history
setopt share_history # share command history data


## Grep
#
export GREP_OPTIONS='--color=auto'


## Misc
#
setopt list_packed       # compacked complete list display
setopt noautoremoveslash # no remove postfix slash of command line
setopt no_beep           # no beep sound when complete list displayed
setopt ignoreeof         # prohibit Ctrl-D to logout


## Functions
#
function chpwd() { ls }

function git_prompt_info() {
  ref=$(git symbolic-ref HEAD 2> /dev/null) || return
  echo "$ZSH_THEME_GIT_PROMPT_PREFIX${ref#refs/heads/}$(parse_git_dirty)$ZSH_THEME_GIT_PROMPT_SUFFIX"
}

parse_git_dirty() {
  local SUBMODULE_SYNTAX=''
  if [[ $POST_1_7_2_GIT -gt 0 ]]; then
        SUBMODULE_SYNTAX="--ignore-submodules=dirty"
  fi
  if [[ -n $(git status -s ${SUBMODULE_SYNTAX}  2> /dev/null) ]]; then
    echo "$ZSH_THEME_GIT_PROMPT_DIRTY"
  else
    echo "$ZSH_THEME_GIT_PROMPT_CLEAN"
  fi
}


## load user .zshrc configuration file
#
[ -f ${HOME}/.zshrc.mine ] && source ${HOME}/.zshrc.mine