## Alias
#
case "${OSTYPE}" in
freebsd*|darwin*)
  alias ls="ls -G -w"
  ;;
linux*)
  alias ls="ls --color"
  alias open="xdg-open"
  ;;
esac

alias j="jobs -l"

alias l="ls -la"
alias la="ls -a"
alias ll="ls -l"

alias du="du -h"
alias df="df -h"

alias su="su -l"

alias ack="ack-grep"

alias -g L="| less"
alias -g G='| grep'
alias -g C='| cat -n'
alias -g W='| wc'
alias -g H='| head'
alias -g T='| tail'

alias _='sudo'


## Completion
#
setopt complete_aliases
fpath=(${HOME}/.zsh/functions/Completion ${fpath})
autoload -U compinit
compinit


## Environment variables
#
export LANG=ja_JP.UTF-8
case ${UID} in
0)
  LANG=C
  ;;
esac
export EDITOR=vi


## Theme and appearance
#
autoload -Uz colors
autoload -Uz vcs_info
colors
setopt prompt_subst
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':vcs_info:*' formats '[%b]'
zstyle ':vcs_info:*' actionformats '[%b|%a]'
precmd () {
    psvar=()
    LANG=en_US.UTF-8 vcs_info
    [[ -n "$vcs_info_msg_0_" ]] && psvar[1]="$vcs_info_msg_0_"
}

PROMPT=$'%{$fg_bold[green]%}%n@%m:%~%{$reset_color%} %{$fg[blue]%}%D{[%T]} %{$reset_color%}%1(v|%F{green}%1v%f|)\
%{$fg_bold[blue]%}%#%{$reset_color%} '


## Directories
#
setopt auto_name_dirs
setopt auto_pushd
setopt pushd_ignore_dups
alias -g ...='../..'
alias -g ....='../../..'


## Keybind configuration
#

# emacs like keybind (e.x. Ctrl-a gets to line head and Ctrl-e gets
# to end) and something additions
bindkey -e
bindkey "^[[1~" beginning-of-line # Home gets to line head
bindkey "^[[4~" end-of-line # End gets to line end
bindkey "^[[3~" delete-char # Del

# historical backward/forward search with linehead string binded to ^P/^N
autoload history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^p" history-beginning-search-backward-end
bindkey "^n" history-beginning-search-forward-end
bindkey "\\ep" history-beginning-search-backward-end
bindkey "\\en" history-beginning-search-forward-end

# reverse menu completion binded to Shift-Tab
bindkey "\e[Z" reverse-menu-complete


## History
#
HISTFILE=$HOME/.zsh_history
HISTSIZE=10000
SAVEHIST=10000

setopt append_history
setopt extended_history
setopt hist_expire_dups_first
setopt hist_ignore_dups # ignore duplication command history list
setopt hist_ignore_space # Lines which begin with a space don't go into the history
setopt hist_verify
setopt inc_append_history
setopt share_history # share command history data
setopt hist_no_store # Not to store history command
setopt hist_reduce_blanks # Remove any excess blanks
setopt hist_ignore_all_dups # Removes copies of lines

## Grep
#
export GREP_OPTIONS='--color=auto'


## Misc
#
setopt list_packed       # compacked complete list display
setopt noautoremoveslash # no remove postfix slash of command line
setopt no_beep           # no beep sound when complete list displayed
setopt ignoreeof         # prohibit Ctrl-D to logout


## Functions
#
function chpwd() { ls }


## cdr
#
autoload -Uz chpwd_recent_dirs cdr add-zsh-hook
add-zsh-hook chpwd chpwd_recent_dirs
zstyle ':chpwd:*' recent-dirs-max 5000
zstyle ':chpwd:*' recent-dirs-default yes
zstyle ':completion:*' recent-dirs-insert both

## peco
#
for f (~/.zsh/peco-sources/*) source "${f}"


## load user .zshrc configuration file
#
[ -f ${HOME}/.zshrc.mine ] && source ${HOME}/.zshrc.mine
